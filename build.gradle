buildscript {
  dependencies { classpath 'org.kohsuke:github-api:1.301' }
}

plugins {
  id 'java-library'
  id 'idea'
  id 'eclipse'

  id 'fabric-loom' version '0.11-SNAPSHOT' apply false
  id 'maven-publish'

  id 'com.modrinth.minotaur' version '2.0.2'
  id 'com.matthewprenger.cursegradle' version '1.4.0'
}

def ENV = System.getenv()

String ver = "${project.version_id}+${project.github_branch}"
version = ENV.GITHUB_ACTIONS ? "${ver}.build.${ENV.GITHUB_RUN_NUMBER}" : ver
logger.lifecycle("Building ${project.mod_id}: ${version}")

def getSubprojectVersion(project) {
  def version = project.properties["${project.name}-version"]
  if (!version) throw new NullPointerException("Could not find version for ${project.name}")
  return version
}

def moduleDependencies(project, List<String> depNames) {
  def deps = depNames.iterator().collect { project.dependencies.project(path: ":$it", configuration: 'namedElements') }
  project.dependencies { deps.each { api it } }
}

allprojects {
  apply plugin: 'java-library'
  apply plugin: 'maven-publish'
  apply plugin: 'fabric-loom'

  tasks.withType(JavaCompile).configureEach { it.options.release = 17 }
  group = project.maven_group

  sourceSets {
    testmod {
      compileClasspath += main.compileClasspath
      runtimeClasspath += main.runtimeClasspath
    }
  }

  loom {
    runtimeOnlyLog4j = true

    runs {
      testmodClient {
        client()
        ideConfigGenerated project.rootProject == project
        name = 'Testmod Client'
        source sourceSets.testmod
      }

      testmodServer {
        server()
        ideConfigGenerated project.rootProject == project
        name = 'Testmod Server'
        source sourceSets.testmod
      }
    }
  }

  repositories {
    maven {
      name = 'Terraformers'
      url  = 'https://maven.terraformersmc.com/releases/'
    }

    maven {
      name = 'Modding Playground'
      url  = 'https://raw.githubusercontent.com/moddingplayground/maven/main/'
    }

    mavenLocal()
  }

  dependencies {
    minecraft "com.mojang:minecraft:$project.ver_minecraft"
    mappings "net.fabricmc:yarn:${project.ver_minecraft}+build.${project.ver_yarn}:v2"
    modApi "net.fabricmc:fabric-loader:${project.ver_loader}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.ver_fabric}"
    modImplementation "com.terraformersmc:modmenu:${project.ver_mod_menu}"
  }

  loom { shareRemapCaches = true }

  processResources {
    inputs.property 'version', project.version
    filesMatching('fabric.mod.json') { expand 'version': project.version }
  }

  java { withSourcesJar() }
  jar { from('README.md', 'LICENSE', 'LICENSE_ASSETS') exclude('.cache') }

  tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
  }

  tasks.withType(GenerateModuleMetadata) { enabled = false }
}

// Apply auxiliary buildscripts to submodules
// This must be done after all plugins are applied to subprojects
apply from: 'gradle/module-versioning.gradle'

subprojects {
  dependencies { testmodImplementation sourceSets.main.output }

  publishing {
    publications { mavenJava(MavenPublication) { from components.java } }
    setupRepositories(repositories)
  }

  afterEvaluate {
    // Disable the gen sources task on sub projects
    genSourcesWithFernFlower.enabled = false
    genSourcesWithCfr.enabled = false
    unpickJar.enabled = false
  }
}

task remapMavenJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: jar) {
  input = jar.archiveFile
  archiveFileName = "${archivesBaseName}-${project.version}-maven.jar"
  addNestedDependencies = false
}
build.dependsOn remapMavenJar

subprojects.each { remapJar.dependsOn("${it.path}:remapJar") }
sourceSets { testmod }

def devOnlyModules = []
dependencies {
  afterEvaluate {
    subprojects.each {
      api project(path: ":${it.name}", configuration: 'namedElements')
      if (!(it.name in devOnlyModules)) include project("${it.name}:")
      testmodImplementation project("${it.name}:").sourceSets.testmod.output
    }
  }
}


/* Publishing */

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact(remapMavenJar) { builtBy remapMavenJar }
      artifact(sourcesJar) { builtBy remapSourcesJar }

      pom.withXml {
        def depsNode = asNode().appendNode('dependencies')
        subprojects.each {
          def depNode = depsNode.appendNode('dependency')
          depNode.appendNode('groupId', it.group)
          depNode.appendNode('artifactId', it.name)
          depNode.appendNode('version', it.version)
          depNode.appendNode('scope', 'compile')
        }
      }
    }
  }

  setupRepositories(repositories)
}

// Required until the deprecation is removed. Frame's main jar that is published to maven does not contain sub modules.
loom.disableDeprecatedPomGeneration(publishing.publications.mavenJava)

void setupRepositories(RepositoryHandler repositories) {
  //repositories.mavenLocal() // uncomment for testing
  def ENV = System.getenv()
  if (ENV.MAVEN_URL) {
    repositories.maven {
      url ENV.MAVEN_URL
      if (ENV.MAVEN_USERNAME) {
        credentials {
          username ENV.MAVEN_USERNAME
          password ENV.MAVEN_PASSWORD
        }
      }
    }
  }
}

task releaseVersion(dependsOn: [ build, 'github', 'modrinth', 'curseforge' ])

import org.kohsuke.github.*
task github(dependsOn: build) {
  onlyIf { ENV.GITHUB_TOKEN }
  doLast {
    def github = GitHub.connectUsingOAuth(ENV.GITHUB_TOKEN)
    def repository = github.getRepository(project.github_repository)

    def builder = new GHReleaseBuilder(repository, version)
    builder.name("[$project.github_branch] $project.mod_name $project.version_id")
    builder.body(new File("./gradle", "CHANGELOG.md").text)
    builder.commitish(project.github_branch)
    builder.prerelease(project.release_type == 'beta')
    builder.create().uploadAsset(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar"), "application/java-archive");
  }
}

import com.modrinth.minotaur.dependencies.*
if (ENV.MODRINTH_TOKEN) modrinth {
  token = ENV.MODRINTH_TOKEN
  projectId = project.modrinth_id
  versionNumber = version
  versionName = "[$project.github_branch] $project.mod_name $project.version_id"
  versionType = project.release_type
  changelog = new File("./gradle", "CHANGELOG.md").text
  uploadFile = remapJar
  gameVersions = [ project.ver_minecraft ]
  dependencies = [
          new ModDependency("P7dR8mSH", DependencyType.REQUIRED)
  ]
}

if (ENV.CURSEFORGE_API_KEY) curseforge {
  apiKey = ENV.CURSEFORGE_API_KEY

  project {
    id = project.curseforge_id
    addGameVersion project.curseforge_version
    addGameVersion 'Fabric'

    changelog = new File("./gradle", "CHANGELOG.md").text
    releaseType = project.release_type

    mainArtifact(remapJar) {
      displayName = "[$project.github_branch] $project.mod_name $project.version_id"
      relations { requiredLibrary 'fabric-api' }
    }

    afterEvaluate { uploadTask.dependsOn(remapJar) }
  }

  options { forgeGradleIntegration = false }
}
